prototype(Neos.NeosIo.ServiceOfferings:Document.ServiceProviderCollection) < prototype(Neos.NeosIo:DefaultPage) {
    body {
        content {
            main  = Neos.Fusion:Component {
                serviceProviders = ${q(node).children('[instanceof Neos.NeosIo.ServiceOfferings:Document.ServiceProvider]')}

                renderer = Neos.Fusion:Component {
                    @apply.props = ${props}

                    serviceProvidersData = Neos.Fusion:Map {
                        items = ${props.serviceProviders}
                        itemName = 'serviceProvider'
                        itemRenderer = Neos.Fusion:DataStructure {
                            title = ${q(serviceProvider).property('title')}
                            description = ${q(serviceProvider).property('description') || ''}
                            website = ${q(serviceProvider).property('website') || ''}
                            logo = Neos.Neos:ImageUri {
                                asset = ${q(serviceProvider).property('logo')}
                                @if.hasLogo = ${q(serviceProvider).property('logo')}
                            }
                            typesOfService = ${q(node).property('typesOfService') || []}
                            showCases = ${Array.length(q(node).property('showCases') || [])}
                            caseStudies = ${Array.length(q(node).property('caseStudies') || [])}
                            packages = ${Array.length(q(node).property('packages') || [])}
                            vendors = ${Array.length(q(node).property('vendors') || [])}
                            badges = ${Array.length(q(node).property('badges') || [])}
                        }
                    }

                    mapData = Neos.NeosIo.ServiceOfferings:Component.MapData {
                        serviceProviders = ${props.serviceProviders}
                    }

                    renderer = afx`
                        <div class="service-provider-collection" data-providerData={Json.stringify(props.serviceProvidersData)}>
                            <WebExcess.OpenStreetMap:Map.Component
                                    json={Json.stringify(props.mapData)}
                                    mapHooks.addMarkersLayerHook="clusterMarkersHook"
                            />
                            <Neos.NeosIo.ServiceOfferings:Component.ProviderList serviceProviders={props.serviceProviders}/>
                        </div>
                    `
                }
            }
        }
    }

    @cache {
        entryTags.children = ${Neos.Caching.descendantOfTag(footerContainer)}
    }
}
