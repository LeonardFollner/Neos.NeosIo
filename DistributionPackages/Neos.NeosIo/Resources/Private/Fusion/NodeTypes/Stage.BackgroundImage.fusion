prototype(Neos.NeosIo:Stage.BackgroundImage) < prototype(Neos.Fusion:Component) {
    title = ${q(node).property('title')}
    title.@process.filenameFallback = ${String.isBlank(value) ? q(node).property('backgroundImage').label : value}

    altText = ${q(node).property('alternativeText')}
    altText.@process.filenameFallback = ${String.isBlank(value) ? q(node).property('backgroundImage').label : value}

    backgroundShadePercentage = ${q(node).property('backgroundShadePercentage')}
    backgroundImage = ${q(node).property('backgroundImage')}

    renderer = Neos.Fusion:Tag {
        tagName = 'img'
        attributes {
            class = Neos.Fusion:DataStructure {
                main = 'stage__bg'
                shade = ${'stage__bg--shade-' + props.backgroundShadePercentage}
                shade.@if.hasShadeActive = ${props.backgroundShadePercentage}
            }
            title = ${props.title}
            alt = ${props.altText}
            src = Neos.Neos:ImageUri {
                asset = ${props.backgroundImage}
                maximumWidth = 5
                maximumHeight = 5
            }
            data-src = Neos.Neos:ImageUri {
                asset = ${props.backgroundImage}
                maximumWidth = 1280
                maximumHeight = 600
            }
            data-srcset = Neos.Fusion:Template {
                templatePath = 'resource://Neos.NeosIo/Private/Templates/NodeTypes/Stage.html'
                sectionName = 'imageSrcSet'
                backgroundImage = ${props.backgroundImage}
            }
            data-component = 'ProgressiveImage'
        }
    }

    @if.hasBackgroundImage = ${this.backgroundImage}
}

