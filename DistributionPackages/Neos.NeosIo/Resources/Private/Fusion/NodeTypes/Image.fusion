prototype(Neos.NodeTypes:Image) {
    partialRootPath = 'resource://Neos.NeosIo/Private/Templates/NodeTypes/Partials'

    title.@process.filenameFallback = ${String.isBlank(value) ? node.properties.image.label : value}

    alternativeText = ${node.properties.alternativeText}
    alternativeText {
        @process.titleFallback = ${String.isBlank(value) ? node.properties.title : value}
        @process.filenameFallback = ${String.isBlank(value) ? node.properties.image.label : value}
    }

    imageClassName {
        @process.addImageStyle = ${value + ' ' + q(node).property('imageStyle')}
        @process.addImageStyle.@if.isSet = ${!String.isBlank(q(node).property('imageStyle'))}
    }

    maximumHeight = null
    // The maximum page width
    maximumWidth = ${1096 * 2}

    maximumHeight.@process.override = ${q(node).property('customHeight') || value}
    maximumWidth.@process.override = ${q(node).property('customHeight') ? null : value}

    link.@process.convertUris {
        absolute = true
    }

    @context {
        image = ${this.image}
        title = ${this.title}
        alternativeText = ${this.alternativeText}
        imageClassName = ${this.imageClassName}
        maximumHeight = ${this.maximumHeight}
        maximumWidth = ${this.maximumWidth}
        allowUpScaling = ${this.allowUpScaling}
        allowCropping = ${this.allowCropping}
    }

    imageTag = Neos.NeosIo:Component.LazyLoadImage {
        image = ${image}
        title = ${title}
        alternativeText = ${alternativeText}
        className = ${imageClassName}
        maximumHeight = ${maximumHeight}
        maximumWidth = ${maximumWidth}
        allowUpScaling = ${allowUpScaling}
        allowCropping = ${allowCropping}
    }
}
