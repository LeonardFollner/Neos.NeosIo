prototype(Neos.NeosIo:Post.Document) < prototype(Neos.NeosIo:DefaultPage) {
    head {
        twitterCard = TYPO3.TypoScript:Array {
            @if.isEnabledBlank = 1
            @if.isEnabled = 1

            cardTypeTag.attributes.content = 'summary'

            cardTitleTag {
                attributes.content = ${String.crop(String.stripTags(q(node).property('title')), 67, '...')}
                @if.isNotBlank = 1
            }

            cardDescriptionTag {
                attributes.content = ${String.crop(String.stripTags(q(node).property('summary')) ? String.stripTags(q(node).property('summary')) : q(node).property('metaDescription'), 197, '...')}
                @if.isNotBlank = 1
            }

            cardImageTag {
                attributes.content.asset = ${q(node).property('image')}
                @if.isImageSet = ${q(node).property('image') != null}
            }
        }

        openGraphMetaTags {
            @context.openGraphType = 'article'

            openGraphTitleTag {
                @context.openGraphTitle = ${String.crop(String.stripTags(q(node).property('title')), 67, '...')}
            }

            openGraphDescriptionTag {
                @context.openGraphDescription = ${String.crop(String.stripTags(q(node).property('summary')) ? String.stripTags(q(node).property('summary')) : q(node).property('metaDescription'), 152, '...')}
            }

            openGraphImageTag {
                @context.openGraphImage = ${q(node).property('image')}
            }

            openGraphPublishedTime = TYPO3.TypoScript:Tag {
                tagName = 'meta'
                attributes {
                    property = 'article:published_time'
                    content = ${Date.format(q(node).property('datePublished'), 'c')}
                }
            }
        }
    }

    body {
        templatePath = 'resource://Neos.NeosIo/Private/Templates/Page/Post.html'
        title = ${q(node).property('title')}
        image = ${q(node).property('image')}
        content {
            // The default content section
            main = Neos.NeosIo:Post {
                templatePath = 'resource://Neos.NeosIo/Private/Templates/Page/Post.html'
                sectionName = 'post'
                content = PrimaryContent {
                    nodePath = 'main'
                }

                keyVisualImage = ${q(node).property('image')}
                latest = ${q(site).find('[instanceof Neos.NeosIo:Post]').slice(0, 6).sort('datePublished', 'DESC').get()}
            }
        }
    }
}
